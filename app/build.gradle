apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    namespace 'de.dotwee.micropinner'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 33

        applicationId "com.lej77.micropinner"
        versionCode 41
        versionName "v2.4.5"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        def keystoreFilePath = System.getenv("SIGNING_STORE_FILE") ?: "my-release-key.keystore"

        if (file(keystoreFilePath).exists()) {
            release {
                storeFile file(keystoreFilePath)
                storePassword System.getenv("SIGNING_STORE_PASSWORD")
                keyAlias System.getenv("SIGNING_KEY_ALIAS")
                keyPassword System.getenv("SIGNING_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
        }

        release {
            // Apply signingConfig only if it was configured
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        incremental false
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.5.0') {
        // Necessary if your app targets Marshmallow (since Espresso
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation('androidx.test.espresso:espresso-intents:3.5.0') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation('androidx.test.ext:junit:1.1.4') {
        // Necessary if your app targets Marshmallow (since the test runner
        // hasn't moved to Marshmallow yet)
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}